# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build_overrides/build.gni")

source_set("cast") {
  sources = [
    "big_endian.h",
    "clock_drift_smoother.cc",
    "clock_drift_smoother.h",
    "constants.h",
    "encoded_frame.cc",
    "encoded_frame.h",
    "expanded_value_base.h",
    "frame_crypto.cc",
    "frame_crypto.h",
    "frame_id.cc",
    "frame_id.h",
    "ntp_time.cc",
    "ntp_time.h",
    "packet_parser.cc",
    "packet_parser.h",
    "rtp_defines.cc",
    "rtp_defines.h",
    "rtp_packetizer.cc",
    "rtp_packetizer.h",
    "rtp_time.cc",
    "rtp_time.h",
    "ssrc.cc",
    "ssrc.h",
  ]

  public_deps = [
    "../../third_party/abseil",
  ]
  if (build_with_chromium) {
    public_deps += [ "//third_party/boringssl" ]
  } else {
    public_deps += [ "../../third_party/boringssl" ]
  }

  deps = [
    "../../platform",
  ]
}

source_set("cast_unittests") {
  testonly = true

  sources = [
    "big_endian_unittest.cc",
    "expanded_value_base_unittest.cc",
    "frame_crypto_unittest.cc",
    "ntp_time_unittest.cc",
    "packet_parser_unittest.cc",
    "rtp_packetizer_unittest.cc",
    "rtp_time_unittest.cc",
    "ssrc_unittest.cc",
  ]

  deps = [
    ":cast",
    "../../third_party/googletest:gtest",
  ]
}

if (build_with_chromium) {
  import("//testing/libfuzzer/fuzzer_test.gni")

  fuzzer_test("cast_packet_parser_fuzzer") {
    sources = [
      "packet_parser_fuzzer.cc",
    ]

    deps = [
      ":cast",
      "../../third_party/abseil",
    ]

    seed_corpus = "packet_parser_fuzzer_seeds"

    # Note: 1500 is approx. kMaxRtpPacketSize in rtp_defines.h.
    libfuzzer_options = [ "max_len=1500" ]
  }
} else {
  # Note: The following is commented out because, as of this writing, the LLVM
  # toolchain we pull does not include libclang_rt.fuzzer-x86_64.a, the
  # libFuzzer library *with* a main() to drive everything. Thus, the only way to
  # get things working is to specify an exact path to the fuzzer_no_main variant
  # of the library that *is* avalable, and then provide our own main(). In
  # summary, what you see below demonstrates how to get it working specifically
  # for Clang 9.0.0 on Linux x86_64. One need only modify the "libs = [...]" for
  # a different Clang, OS, or architecture.

  # if (is_clang) {
  #   executable("cast_packet_parser_fuzzer") {
  #     testonly = true
  #     defines = [ "NEEDS_MAIN_TO_CALL_FUZZER_DRIVER" ]
  #     sources = [
  #       "packet_parser_fuzzer.cc",
  #     ]
  #     cflags_cc = [ "-fsanitize=address,fuzzer-no-link,undefined" ]
  #     ldflags = [ "-fsanitize=address,undefined" ]
  #     libs = [ "$clang_base_path/lib/clang/9.0.0/lib/linux/libclang_rt.fuzzer_no_main-x86_64.a" ]
  #     deps = [
  #       ":cast",
  #       "../../third_party/abseil",
  #     ]
  #   }
  # }
}
