# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build_overrides/build.gni")
import("//third_party/protobuf/proto_library.gni")

source_set("mdns") {
  sources = [
    "mdns/mdns_querier.cc",
    "mdns/mdns_querier.h",
    "mdns/mdns_reader.cc",
    "mdns/mdns_reader.h",
    "mdns/mdns_receiver.cc",
    "mdns/mdns_receiver.h",
    "mdns/mdns_record_changed_callback.h",
    "mdns/mdns_records.cc",
    "mdns/mdns_records.h",
    "mdns/mdns_sender.cc",
    "mdns/mdns_sender.h",
    "mdns/mdns_service_impl.cc",
    "mdns/mdns_service_impl.h",
    "mdns/mdns_trackers.cc",
    "mdns/mdns_trackers.h",
    "mdns/mdns_writer.cc",
    "mdns/mdns_writer.h",
    "mdns/public/mdns_constants.h",
    "mdns/public/mdns_service.h",
  ]
  deps = [
    "../../platform",
    "../../third_party/abseil",
  ]
  configs += [ "../../build:allow_build_from_embedder" ]
}

source_set("certificate") {
  sources = [
    "certificate/cast_cert_validator.cc",
    "certificate/cast_cert_validator.h",
    "certificate/cast_cert_validator_internal.cc",
    "certificate/cast_cert_validator_internal.h",
    "certificate/cast_crl.cc",
    "certificate/cast_crl.h",
    "certificate/types.cc",
    "certificate/types.h",
  ]
  public_deps = [
    "../../third_party/boringssl",
  ]

  deps = [
    ":certificate_proto",
    "../../platform",
    "../../third_party/abseil",
    "../../util",
  ]
}

proto_library("certificate_proto") {
  sources = [
    "certificate/proto/revocation.proto",
  ]
}

source_set("channel") {
  sources = [
    "channel/cast_socket.cc",
    "channel/cast_socket.h",
    "channel/message_framer.cc",
    "channel/message_framer.h",
    "channel/message_util.h",
  ]

  deps = [
    ":certificate_proto",
    ":channel_proto",
    "../../util",
  ]

  public_deps = [
    "../../platform",
    "../../third_party/abseil",
  ]
}

proto_library("channel_proto") {
  sources = [
    "channel/proto/cast_channel.proto",
  ]
}

proto_library("certificate_unittest_proto") {
  testonly = true
  sources = [
    "certificate/proto/test_suite.proto",
  ]
}

source_set("unittests") {
  testonly = true
  sources = [
    "certificate/cast_cert_validator_unittest.cc",
    "certificate/cast_crl_unittest.cc",
    "certificate/test_helpers.cc",
    "certificate/test_helpers.h",
    "channel/cast_socket_unittest.cc",
    "channel/message_framer_unittest.cc",
    "mdns/mdns_querier_unittest.cc",
    "mdns/mdns_random_unittest.cc",
    "mdns/mdns_reader_unittest.cc",
    "mdns/mdns_receiver_unittest.cc",
    "mdns/mdns_records_unittest.cc",
    "mdns/mdns_sender_unittest.cc",
    "mdns/mdns_trackers_unittest.cc",
    "mdns/mdns_writer_unittest.cc",
  ]

  deps = [
    ":certificate",
    ":certificate_unittest_proto",
    ":channel",
    ":mdns",
    "../../platform",
    "../../third_party/boringssl",
    "../../third_party/googletest:gmock",
    "../../third_party/googletest:gtest",
    "../../util",
  ]
}
