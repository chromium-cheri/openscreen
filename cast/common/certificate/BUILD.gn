# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build_overrides/build.gni")

source_set("certificate") {
  sources = [
    "cast_cert_validator.cc",
    "cast_cert_validator.h",
    "cast_cert_validator_internal.cc",
    "cast_cert_validator_internal.h",
    "cast_crl.cc",
    "cast_crl.h",
    "types.cc",
    "types.h",
  ]
  public_deps = [
    "../../../third_party/boringssl",
  ]

  deps = [
    "../../../platform",
    "../../../third_party/abseil",
    "../../../util",
    "proto",
  ]
}

source_set("unittests") {
  testonly = true

  # TODO(jophba): allow test certificate lookup from the embedder, so this can
  # run in Chrome.
  if (!build_with_chromium) {
    sources = [
      "test_helpers.cc",
      "test_helpers.h",
    ]

    # Both of these tests require access to the test certificates folder.
    sources += [
      "cast_cert_validator_unittest.cc",
      "cast_crl_unittest.cc",
    ]

    deps = [
      ":certificate",
      "../../../platform",
      "../../../third_party/boringssl",
      "../../../third_party/googletest:gtest",
      "proto:unittest_proto",
    ]
  }
}
