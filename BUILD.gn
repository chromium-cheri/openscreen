# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build_overrides/build.gni")
import("osp/build/config/services.gni")

config("root_config_overrides") {
  if (build_with_chromium) {
    include_dirs = [ "." ]
  }
}

# All compilable non-test targets in the repository (both executables and
# source_sets).
group("gn_all") {
  deps = [
    "base",
    "osp",
    "osp/msgs",
    "platform",
    "third_party/abseil",
    "third_party/tinycbor",
    "tools/cddl",
  ]

  if (use_mdns_responder) {
    deps += [ "osp/impl/discovery/mdns:embedder_demo" ]
  }

  if (use_chromium_quic) {
    deps += [
      "third_party/chromium_quic",
      "third_party/chromium_quic:demo_client",
      "third_party/chromium_quic:demo_server",
    ]
  }
}

executable("openscreen_unittests") {
  testonly = true
  deps = [
    "base:base_unittests",
    "osp/impl/discovery/mdns:mdns_unittests",
    "osp/msgs:unittests",
    "osp:osp_unittests",
    "platform:platform_unittests",
    "streaming/cast:cast_unittests",
    "third_party/abseil",
    "third_party/googletest:gtest_main",
  ]
}

if (use_chromium_quic && use_mdns_responder) {
  executable("demo") {
    sources = [
      "osp/demo/demo.cc",
    ]

    deps = [
      "//osp:osp_with_chromium_quic",
      "//osp/public",
      "//base",
      "//osp/impl/discovery/mdns",
      "//platform",
    ]
  }
}
