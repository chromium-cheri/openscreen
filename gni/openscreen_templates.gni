# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/config/compiler/pgo/pgo.gni")
import("//build/config/gclient_args.gni")
import("//build/config/ios/config.gni")
import("//build/config/sanitizers/sanitizers.gni")
import("//build_overrides/build.gni")
import("split_static_library.gni")

declare_args() {
  # Use static libraries instead of source_sets.
  openscreen_static_library = false

  # Enable monolithic static library for embedders.
  openscreen_monolithic = false

  # Override global symbol level setting for Open Screen.
  openscreen_symbol_level = symbol_level
}

# Points to // in openscreen stand-alone or to //third_party/openscreen/ in Chrome. We need absolute
# paths for all configs in templates as they are shared in different
# subdirectories.
openscreen_path_prefix = get_path_info("../", "abspath")

###############################################################################
# Templates
#

# Common configs to remove or add in all openscreen targets.
openscreen_remove_configs = []
openscreen_add_configs = [
  openscreen_path_prefix + ":features",
  openscreen_path_prefix + ":toolchain",
  openscreen_path_prefix + ":strict_warnings",
]

if (is_debug) {
  openscreen_remove_configs +=
      [ "//build/config/compiler:default_optimization" ]
  openscreen_add_configs += [ "//build/config/compiler:no_optimize" ]
} else {
  openscreen_remove_configs +=
      [ "//build/config/compiler:default_optimization" ]

  if (is_posix && !is_android && !using_sanitizer) {
    openscreen_add_configs += [ "//build/config/compiler:optimize_speed" ]
  } else {
    openscreen_add_configs += [ "//build/config/compiler:optimize_max" ]
  }
}

if (!is_debug) {
  openscreen_remove_configs += [
    # Too much performance impact, unclear security benefit.
    "//build/config/compiler:default_init_stack_vars",
  ]
}

if (openscreen_symbol_level != symbol_level) {
  openscreen_remove_configs += [ "//build/config/compiler:default_symbols" ]
  if (openscreen_symbol_level == 0) {
    openscreen_add_configs += [ "//build/config/compiler:no_symbols" ]
  } else if (openscreen_symbol_level == 1) {
    openscreen_add_configs += [ "//build/config/compiler:minimal_symbols" ]
  } else if (openscreen_symbol_level == 2) {
    openscreen_add_configs += [ "//build/config/compiler:symbols" ]
  } else {
    assert(false)
  }
}

if (!build_with_chromium && is_clang) {
  openscreen_remove_configs += [ "//build/config/clang:find_bad_constructs" ]
}

# All templates should be kept in sync.
template("openscreen_source_set") {
  if (defined(invoker.split_count) && invoker.split_count > 1 &&
      defined(openscreen_static_library) && openscreen_static_library &&
      is_win) {
    link_target_type = "split_static_library"
  } else if (defined(openscreen_static_library) && openscreen_static_library) {
    link_target_type = "static_library"
  } else {
    link_target_type = "source_set"
  }
  target(link_target_type, target_name) {
    forward_variables_from(invoker,
                           "*",
                           [
                             "configs",
                             "remove_configs",
                           ])
    configs -= openscreen_remove_configs
    configs += openscreen_add_configs
    if (defined(invoker.remove_configs)) {
      configs -= invoker.remove_configs
    }
    if (defined(invoker.configs)) {
      configs += invoker.configs
    }
  }
}

template("openscreen_header_set") {
  source_set(target_name) {
    forward_variables_from(invoker, "*", [ "configs" ])
    configs -= openscreen_remove_configs
    configs += openscreen_add_configs
    configs += invoker.configs
  }
}

template("openscreen_executable") {
  if (is_ios) {
    import("//build/config/ios/rules.gni")

    ios_app_bundle(target_name) {
      forward_variables_from(invoker,
                             "*",
                             [
                               "configs",
                               "remove_configs",
                             ])
      configs -= openscreen_remove_configs
      configs += openscreen_add_configs
      if (defined(invoker.remove_configs)) {
        configs -= invoker.remove_configs
      }
      configs += invoker.configs

      # Provide sensible defaults in case invoker did not define any of those
      # required variables.
      if (!defined(info_plist) && !defined(info_plist_target)) {
        info_plist = openscreen_path_prefix + "/gni/Info.plist"
      }

      bundle_identifier = "$ios_app_bundle_id_prefix.chrome.unittests.dev"
    }
  } else {
    executable(target_name) {
      forward_variables_from(invoker,
                             "*",
                             [
                               "configs",
                               "remove_configs",
                             ])
      configs -= openscreen_remove_configs
      configs += openscreen_add_configs
      if (defined(invoker.remove_configs)) {
        configs -= invoker.remove_configs
      }
      if (defined(invoker.configs)) {
        configs += invoker.configs
      }
      if (is_linux || is_chromeos) {
        # For enabling ASLR.
        ldflags = [ "-pie" ]
      }

      if (is_win && !openscreen_enable_cet_shadow_stack) {
        configs -= [ "//build/config/compiler:cet_shadow_stack" ]
      }
    }
  }
}

template("openscreen_component") {
  component(target_name) {
    output_name = target_name
    forward_variables_from(invoker,
                           "*",
                           [
                             "configs",
                             "remove_configs",
                           ])
    configs -= openscreen_remove_configs
    configs += openscreen_add_configs
    if (defined(invoker.remove_configs)) {
      configs -= invoker.remove_configs
    }
    configs += invoker.configs
  }
}

template("openscreen_shared_library") {
  shared_library(target_name) {
    forward_variables_from(invoker,
                           "*",
                           [
                             "configs",
                             "remove_configs",
                           ])
    configs -= openscreen_remove_configs
    configs += openscreen_add_configs
    if (defined(invoker.remove_configs)) {
      configs -= invoker.remove_configs
    }
    if (defined(invoker.configs)) {
      configs += invoker.configs
    }
  }
}

template("openscreen_static_library") {
  static_library(target_name) {
    complete_static_lib = true
    forward_variables_from(invoker, "*", [ "configs" ])
    configs -= openscreen_remove_configs
    configs -= [ "//build/config/compiler:thin_archive" ]
    configs += openscreen_add_configs
    configs += invoker.configs
  }
}
